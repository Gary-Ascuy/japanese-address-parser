/**
 * https://docs.gradle.org/8.6/userguide/building_java_projects.html
 */

plugins {
  alias(libs.plugins.jvm)

  id 'java-library'
  id 'maven-publish'
  id 'com.ncorti.ktfmt.gradle' version '0.17.0'
}

group 'io.ziogd.garyascuy'
version getGithubVersion()

repositories {
  mavenCentral()
}

dependencies {
  // Use the Kotlin JUnit 5 integration.
  testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

  // Use the JUnit 5 integration.
  testImplementation libs.junit.jupiter.engine
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  // Use JUnit Json Params
  testImplementation 'net.joshka:junit-json-params:5.10.2-r0'
  testImplementation 'org.eclipse.parsson:parsson:1.1.5'

  // This dependency is exported to consumers, that is to say found on their compile classpath.
  api libs.commons.math3

  // This dependency is used internally, and not exposed to consumers on their own compile classpath.
  implementation libs.guava
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri('https://maven.pkg.github.com/Gary-Ascuy/japanese-address-parser')
      credentials {
        username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
        password = project.findProperty('gpr.key') ?: System.getenv('TOKEN')
      }
    }
  }
  publications {
    mavenGitHubPublication(MavenPublication) {
      from components.java
      artifactId = 'japanese-address-parser'

      pom {
        name = 'Japanese Address Parser'
        description = "Japanese Address Parser Library"
        url = "https://github.com/Gary-Ascuy/japanese-address-parser"
        licenses {
          license {
            name = "MIT License"
            url = "https://github.com/Gary-Ascuy/japanese-address-parser/blob/main/LICENSE"
          }
        }
        developers {
          developer {
            id = "gary-ascuy"
            name = "Gary Ascuy"
            email = "gary.ascuy@gmail.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:Gary-Ascuy/japanese-address-parser.git"
          developerConnection = "scm:git:git@github.com:Gary-Ascuy/japanese-address-parser.git"
          url = "https://github.com/Gary-Ascuy/japanese-address-parser"
        }
        issueManagement {
          system = "GitHub"
          url = "https://github.com/Gary-Ascuy/japanese-address-parser/issues"
        }
      }
    }
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

tasks.named('test') {
  useJUnitPlatform()
}

static def getGithubVersion() {
  var type = System.getenv('GITHUB_REF_TYPE')
  var name = System.getenv('GITHUB_REF_NAME')
  var isValidVersion = type == "tag" && name != null && name.startsWith("v")
  return isValidVersion ? name.replace("v", "") : '1.0.0-SNAPSHOT'
}
